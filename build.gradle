configurations {
    optional
}

dependencies {

    compile(project(":grails-datastore-gorm-hibernate-core")) {
        exclude group:'org.hibernate', module:'hibernate-core'
        exclude group:'org.hibernate', module:'hibernate-validator'
        exclude group:'org.hibernate', module:'hibernate-ehcache'
        exclude group:'org.hibernate', module:'hibernate-commons-annotations'
    }

    // Web dependencies optional
    optional("org.grails:grails-web:$grailsVersion") {
        transitive = false
    }
    optional("org.grails:grails-spring:$grailsVersion") {
        transitive = false
    }
    optional("org.springframework:spring-webmvc:$springVersion") {
        transitive = false
    }

    optional 'javax.servlet:servlet-api:2.5'
    optional(project(":grails-datastore-web"))

    String hibernateVersion = '4.3.8.Final'

    compile("org.hibernate:hibernate-core:$hibernateVersion") {
        exclude group:'commons-logging', module:'commons-logging'
        exclude group:'dom4j', module:'dom4j'
        exclude group:'com.h2database', module:'h2'
        exclude group:'commons-collections', module:'commons-collections'
        exclude group:'org.slf4j', module:'jcl-over-slf4j'
        exclude group:'org.slf4j', module:'slf4j-api'
        exclude group:'org.slf4j', module:'slf4j-log4j12'
        exclude group:'xml-apis', module:'xml-apis'
    }
    compile('org.hibernate.common:hibernate-commons-annotations:4.0.4.Final'){
        exclude group: 'org.slf4j', module:'slf4j-api'
        exclude group: 'commons-logging', module:'commons-logging'
    }
    compile('org.hibernate:hibernate-validator:5.0.3.Final') {
        exclude group:'commons-logging', module:'commons-logging'
        exclude group:'commons-collections', module:'commons-collections'
        exclude group:'org.slf4j', module:'slf4j-api'
    }

    runtime('dom4j:dom4j:1.6.1') {
        exclude group: 'xml-apis', module:'xml-apis'
    }

    optional ("net.sf.ehcache:ehcache-core:2.4.8") {
        exclude group: 'commons-logging', module:'commons-logging'
    }

    optional ("org.hibernate:hibernate-ehcache:$hibernateVersion") {
        exclude group:'commons-collections', module:'commons-collections'
        exclude group:'commons-logging', module:'commons-logging'
        exclude group:'com.h2database', module:'h2'
        exclude group:'dom4j', module:'dom4j'
        exclude group:'net.sf.ehcache', module:'ehcache'
        exclude group:'net.sf.ehcache', module:'ehcache-core'
        exclude group:'org.hibernate', module:'hibernate-core'
        exclude group:'org.slf4j', module:'jcl-over-slf4j'
        exclude group:'org.slf4j', module:'slf4j-api'
        exclude group:'org.slf4j', module:'slf4j-log4j12'
        exclude group:'xml-apis', module:'xml-apis'
    }

    testCompile project(":grails-datastore-gorm-test")
    testCompile project(":grails-datastore-gorm-tck")
    testCompile "com.h2database:h2:1.3.164"

    testCompile "net.sf.ehcache:ehcache-core:2.4.6"
    testCompile "org.hibernate:hibernate-ehcache:$hibernateVersion"

    optional "javax.servlet:servlet-api:2.5"
    def excludes = {
        exclude group:"org.grails",module: "grails-plugin-url-mappings"
        exclude group:"org.grails",module: "grails-plugin-servlets"
        exclude group:"org.grails",module: "grails-plugin-controllers"
        exclude group:"org.grails",module: "grails-plugin-domain-class"
        exclude group:"org.grails",module: "grails-plugin-gsp"
        exclude group:"org.grails",module: "grails-plugin-filters"
        exclude group:"org.grails",module: "grails-plugin-mimetypes"
        exclude group:"org.grails",module: "grails-plugin-converters"
        exclude group:"org.grails",module: "grails-logging"
        exclude group:"org.grails",module: "grails-test"
        exclude group:"org.grails",module: "grails-datastore-gorm"
        exclude group:"org.grails",module: "grails-datastore-core"
        exclude group:"org.grails",module: "grails-datastore-simple"
        exclude group:"org.grails",module: "grails-datastore-gorm"
    }
    optional "org.grails:grails-test:$grailsVersion", excludes
    optional "org.grails:grails-plugin-testing:$grailsVersion", excludes

    def tomcatVersion = '7.0.50'
    testCompile "org.apache.tomcat:tomcat-jdbc:$tomcatVersion"
    testRuntime "org.apache.tomcat.embed:tomcat-embed-logging-log4j:$tomcatVersion"

}

sourceSets {
    main {
        compileClasspath += files(configurations.optional)
    }
    javadoc {
        classpath = files(configurations.compile) + files(configurations.optional)
    }
}
idea {
    module {
        scopes.PROVIDED.plus += [configurations.optional]
    }
}

eclipse {
    classpath {
        plusConfigurations += [configurations.optional]
        file {
            whenMerged { classpath ->
                 // move grails-datastore-gorm-hibernate-core reference to end
                 def hibernateCoreReference = classpath.entries.find { entry -> entry.kind == 'src' && entry.path == "/grails-datastore-gorm-hibernate-core" }
                 if (hibernateCoreReference) {
                     classpath.entries.remove hibernateCoreReference
                     classpath.entries << hibernateCoreReference
                 }
            }
        }
    }
}

test {
    if (!isTravisBuild) {
        forkEvery = 30
    }
}
